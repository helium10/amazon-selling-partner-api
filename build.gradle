plugins {
  id 'java'
  id 'org.hidetake.swagger.generator' version '2.18.2'
}

repositories {
  jcenter()
}

dependencies {
  swaggerCodegen 'io.swagger:swagger-codegen-cli:2.4.2'
}

version = '1.0.2'

def globalProperties = [
  "groupId": "com.helium10.amazon-selling-partner-api",
  "artifactVersion": project.version,
  "hideGenerationTimestamp": true,
  "dateLibrary": "java8",
  "java8": "true"
]

swaggerSources {
  [
    authorization: 'authorization',
    'catalog-items': 'catalogItemsV0',
    'fba-inbound-eligibility': 'fbaInbound',
    'fba-inventory': 'fbaInventory',
    'fba-small-and-light': 'fbaSmallandLight',
    'feeds': 'feeds_2020-09-04',
    'finances': 'financesV0',
    'fulfillment-inbound': 'fulfillmentInboundV0',
    'fulfillment-outbound': 'fulfillmentOutbound_2020-07-01',
    'merchant-fulfillment': 'merchantFulfillmentV0',
    messaging: 'messaging',
    notifications: 'notifications',
    orders: 'ordersV0',
    'product-fees': 'productFeesV0',
    'product-pricing': 'productPricingV0',
    'reports': 'reports_2020-09-04',
    'sales': 'sales',
    'sellers': 'sellers',
    'services': 'services',
    'shipping': 'shipping',
    'solicitations': 'solicitations',
    'uploads': 'uploads_2020-11-01',
  ].forEach { dir, modelFile ->
    "${modelFile.replaceAll('_', '').replaceAll('-', '')}" {
      inputFile = file("./models/${dir}-api-model/${modelFile}.json")
      code {
        language = 'java'
        templateDir = file('./clients/sellingpartner-api-aa-java/resources/swagger-codegen/templates')
        additionalProperties = globalProperties + [
          "artifactId": dir,
          "modelPackage": "com.helium10.spapi.${dir.replaceAll('-', '')}.model",
          "apiPackage": "com.helium10.spapi.${dir.replaceAll('-', '')}.api",
          "invokerPackage": "com.helium10.spapi.${dir.replaceAll('-', '')}"

        ]
      }
    }
  }
}
